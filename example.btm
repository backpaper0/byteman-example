RULE Trace HelloModel construction
CLASS com.example.HelloModel
METHOD <init>
AT ENTRY
IF TRUE
DO
    java.lang.Thread.dumpStack();
ENDRULE

RULE uppercase HelloModel.name at sayHello
CLASS com.example.HelloController
METHOD sayHello(com.example.HelloModel)
AT ENTRY
IF $1 != null && $1.name() != null
DO
  $1 = new com.example.HelloModel($1.name().toUpperCase());
  traceln("[Byteman] name -> " + $1.name());
ENDRULE

RULE add foo->bar to HelloController.sayHello
CLASS com.example.HelloController
METHOD sayHello
AT EXIT
BIND newMap: java.util.Map = ($! == null)
                 ? new java.util.HashMap()
                 : new java.util.HashMap($!);
IF TRUE
DO
    newMap.put("foo", "bar");
    RETURN newMap;
ENDRULE
